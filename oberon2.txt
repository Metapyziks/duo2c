Module       = MODULE ident ";" [ImportList] DeclSeq
               [BEGIN StatementSeq] END ident ".".
ImportList   = IMPORT [ident ":="] ident {"," [ident ":="] ident} ";".
DeclSeq      = { CONST {ConstDecl ";" } | TYPE {TypeDecl ";"}
                 | VAR {VarDecl ";"}} {ProcDecl ";" | ForwardDecl ";"}.
ConstDecl    = IdentDef "=" ConstExpr.
TypeDecl     = IdentDef "=" Type.
VarDecl      = IdentList ":" Type.
ProcDecl     = PROCEDURE [Receiver] IdentDef [FormalPars] ";" DeclSeq
               [BEGIN StatementSeq] END ident.
ForwardDecl  = PROCEDURE "^" [Receiver] IdentDef [FormalPars].
FormalPars   = "(" [FPSection {";" FPSection}] ")" [":" Qualident].
FPSection    = [VAR] ident {"," ident} ":" Type.
Receiver     = "(" [VAR] ident ":" ident ")".
Type         = ARRAY [ConstExpr {"," ConstExpr}] OF Type
             | RECORD ["("Qualident")"] {FieldList ";"} END
             | POINTER TO Type
             | PROCEDURE [FormalPars]
             | Qualident.
FieldList    = IdentList ":" Type.
StatementSeq = Statement {";" Statement} [";"].
Statement    = IF Expr THEN StatementSeq {ELSIF Expr THEN StatementSeq}
               [ELSE StatementSeq] END 
             | CASE Expr OF Case {"|" Case} [ELSE StatementSeq] END 
             | WHILE Expr DO StatementSeq END 
             | REPEAT StatementSeq UNTIL Expr 
             | FOR ident ":=" Expr TO Expr [BY ConstExpr] DO StatementSeq END 
             | LOOP StatementSeq END
             | WITH Guard DO StatementSeq {"|" Guard DO StatementSeq}
               [ELSE StatementSeq] END
             | EXIT 
             | RETURN [Expr]
             | Designator ":=" Expr 
             | Designator ["(" [ExprList] ")"].
Case         = [CaseLabels {"," CaseLabels} ":" StatementSeq].
CaseLabels   = ConstExpr [".." ConstExpr].
Guard        = Qualident ":" Qualident.
ConstExpr    = Expr.
Expr         = SimpleExpr [Relation SimpleExpr].
SimpleExpr   = ["+" | "-"] Term {AddOp Term}.
Term         = Factor {MulOp Factor}.
Factor       = Number | character | string | NIL | Set | "(" Expr ")"
             | "~" Factor | Designator ["(" [ExprList] ")"].
Set          = "{" [Element {"," Element}] "}".
Element      = Expr [".." Expr].
Relation     = "=" | "#" | "<" | "<=" | ">" | ">=" | IN | IS.
AddOp        = "+" | "-" | OR.
MulOp        = "*" | "/" | DIV | MOD | "&".
Designator   = Qualident {"." ident | "[" ExprList "]" | "^"
             | "(" Qualident ")"}.
ExprList     = Expr {"," Expr}.
IdentList    = IdentDef {"," IdentDef}.
Qualident    = ident ["." ident].
IdentDef     = ident ["*" | "-"].
Number       = real | integer.
integer*     = digit {hexDigit} "H" | digit {digit}.
real*        = digit {digit} "." {digit} [scaleFactor].
scaleFactor  = ("E" | "D") ["+" | "-"] digit {digit}.
hexDigit     = digit | "A" | "B" | "C" | "D" | "E" | "F".
character*   = digit {hexDigit} "X".
commentOpen  = "(*".
commentClose = "*)".
